#!ipxe

goto ${menu} ||

:live_menu
set os_arch ${arch}
iseq ${os_arch} x86_64 && set os_arch amd64 ||
iseq ${os_arch} i386 && set os_arch i686 ||
set os Clonezilla
menu ${os}
item --gap ${os} Versions
item debian ${space} ${os} Debian Based
iseq ${os_arch} amd64 && item ubuntu ${space} ${os} Ubuntu Based ||
choose live_version || goto live_exit
menu ${os} ${live_version} - ${os_arch}
item --gap ${os} Flavors
goto ${live_version}

:debian
iseq ${os_arch} amd64 && item /debian-squash/releases/download/3.3.0-33-9c8061ac/ ${space} Clonezilla 3.3.0-33 Stable ||
iseq ${os_arch} amd64 && item /debian-squash/releases/download/3.3.1-10-47a038f9/ ${space} Clonezilla 3.3.1-10 Testing ||
choose path || goto live_menu
goto clonezilla-boot

:ubuntu
iseq ${os_arch} amd64 && item /ubuntu-squash/releases/download/20251017-questing-c0f6109b/ ${space} Clonezilla 20251017-Questing Stable ||
iseq ${os_arch} amd64 && item /ubuntu-squash/releases/download/20251129-resolute-0d4c667b/ ${space} Clonezilla 20251129-Resolute Testing ||
choose path || goto live_menu
goto clonezilla-boot

:clonezilla-boot
imgfree
set url ${live_endpoint}${path}
kernel ${url}vmlinuz boot=live username=user union=overlay config components noswap edd=on nomodeset ocs_live_run="ocs-live-general" ocs_live_batch=no net.ifnames=0 nosplash noprompt fetch=${url}filesystem.squashfs initrd=initrd.magic ${cmdline}
initrd ${url}initrd
boot

:live_exit
clear menu
exit 0

